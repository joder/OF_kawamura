load 'common.gpi'

epsp(u,v,w)=(u+v+w)
epsil(x)=x/(nu*utau**2/h**2)/Retau**2
epsilon(c)=epsil(column(c))
otoe(o,k)=epsil(column(o)*column(k)*0.09)
col=5


set ylabel '{/Symbol e}^+'

set output 'comp_epsilon.png'
set ytics format "%4.2f"
plot\
  Keps u 1:(-epsp($2,$3,$4)) w @wDNS t 'Kawamura' ls lsDNS \
, for [i=1:N-1] srcp(i) u (yp($1)):(epsilon(col)) w @wMesh t titl(i) ls linstyl(i)\
, for [i=N:N] srcp(i) u (yp($1)):(otoe(col,col-1)) w @wMesh t titl(i) ls linstyl(i)

set output 'comp_epsilon_semilog.png'
set logscale x
plot\
  Keps u 1:(-epsp($2,$3,$4)) w @wDNS t 'Kawamura' ls lsDNS \
, for [i=1:N-1] srcp(i) u (yplog($1)):(epsilon(col)) w @wMesh t titl(i) ls linstyl(i)\
, for [i=N:N] srcp(i) u (yplog($1)):(otoe(col,col-1)) w @wMesh t titl(i) ls linstyl(i)

set output 'comp_epsilon_log.png'
set logscale xy
set ytics 1e-8, 10, 1e8 format "10^{%T}" 
set mytics 10
plot\
  Keps u 1:(-epsp($2,$3,$4)) w @wDNS t 'Kawamura' ls lsDNS \
, for [i=1:N-1] srcp(i) u (yp($1)):(epsilon(col)) w @wMesh t titl(i) ls linstyl(i)\
, for [i=N:N] srcp(i) u (yp($1)):(otoe(col,col-1)) w @wMesh t titl(i) ls linstyl(i)
