load 'common.gpi'

k(u,v,w)=0.5*(u+v+w)
epsp(u,v,w)=(u+v+w)
col=6

Cmu=0.09
fMu(k,ep)=exp(-3.4/((1+k**2/(50*ep))**2))
nutLSKE(k,ep)=Cmu*fMu(k,ep)*k**2/ep
kc(uu,vv,ww,eu,ev,ew)=nutLSKE(k(uu,vv,ww)*utau**2,-epsp(eu,ev,ew)*nu*utau**2/h**2*Retau**2)

set ylabel '{/Symbol n}_t'

set output 'comp_nut.png'
plot\
  Knut u 1:(kc($2,$3,$4,$5,$6,$7)) w @wDNS t 'Kawamura' ls lsDNS \
, for [i=1:N] srcp(i) u (yp($1)):(column(col)) w @wMesh t titl(i) ls linstyl(i)

set output 'comp_nut_semilog.png'
set logscale x
replot

set output 'comp_nut_log.png'
set logscale xy
replot
