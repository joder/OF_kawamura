if(ARGC!=2) {
	print sprintf("Not enough arguments!\nUsage: gnuplot -c \"%s\" line_NAME.xy N\nExiting gnuplot", ARG0)
	exit gnuplot
}
fn=ARG1
col=ARG2+0

load 'common.gpi'
Pr=10

set ylabel 'T^{+} / Pr'

set output 'comp_T10.png'
plot\
  sample [*:10] x notitle ls 5 \
, KT10 u 2:($3/Pr) t 'Kawamura' ls 1 \
, fn u (yp($1)):(subst(column(col))/Pr) t 'Current' ls 2

set output 'comp_T10_semilog.png'
set logscale x
plot\
  sample [*:10] x notitle ls 5 \
, KT10 u 2:($3/Pr) t 'Kawamura' ls 1 \
, fn u (yplog($1)):(subst(column(col))/Pr) t 'Current' ls 2

set output 'comp_T10_log.png'
set logscale xy
plot\
  sample [*:10] x notitle ls 5 \
, KT10 u 2:($3/Pr) t 'Kawamura' ls 1 \
, fn u (yp($1)):(subst(column(col))/Pr) t 'Current' ls 2
