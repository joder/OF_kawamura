if(ARGC!=2) {
	print sprintf("Not enough arguments!\nUsage: gnuplot -c \"%s\" line_NAME.xy N\nExiting gnuplot", ARG0)
	exit gnuplot
}
fn=ARG1
col=ARG2+0

load 'common.gpi'
Pr=0.71

set ylabel 'T^{+}'

set output 'comp_T071.png'
plot\
  sample [*:11] x notitle ls 5 \
, KT071 u 2:($3/Pr) t 'Kawamura' ls 1 \
, fn u (yp($1)):(subst(column(col))/Pr) t 'Current' ls 2

set output 'comp_T071_semilog.png'
set logscale x
plot\
  sample [*:11] x notitle ls 5 \
, KT071 u 2:($3/Pr) t 'Kawamura' ls 1 \
, fn u (yplog($1)):(subst(column(col))/Pr) t 'Current' ls 2

set output 'comp_T071_log.png'
set logscale xy
plot\
  sample [*:11] x notitle ls 5 \
, KT071 u 2:($3/Pr) t 'Kawamura' ls 1 \
, fn u (yp($1)):(subst(column(col))/Pr) t 'Current' ls 2
